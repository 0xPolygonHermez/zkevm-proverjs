
/*
    Equations:

    ===============  256 bits =======================================

    EQ0:   A(x1) * B(y1) + C(x2) = D (y2) * 2 ** 256 + op (y3)   - selEq[0]

    EQ1:   s * x2 - s * x1 - y2 + y1 + (q0 * p1)   lambda - ADD  - selEq[1]
    EQ2:   2 * s * y1 - 3 * x1 * x1 + (q0 * p1)    lambda - DBL  - selEq[2]
    EQ3:   s * s - x1 - x2 - x3 + (q1 * p1)        x3            - selEq[1] + selEq[2]
    EQ4:   s * x1 - s * x3 - y1 - y3 + (q2 * p1)   y3            - selEq[1] + selEq[2]

    EQ5:   x1 * x2 - y1 * y2 - x3 + (q1 * p2)                    - selEq[3]
    EQ6:   y1 * x2 + x1 * y2 - y3 + (q2 * p2)                    - selEq[3]

	EQ7:   x1 + x2 - x3 + (q1 * p2)                              - selEq[4]
	EQ8:   y1 + y2 - y3 + (q2 * p2)                              - selEq[4]

	EQ9:   x1 - x2 - x3 + (q1 * p2)                              - selEq[5]
	EQ10:  y1 - y2 - y3 + (q2 * p2)                              - selEq[5]

    EQ11:  A(x1) * B(y1) + C(x2) - op(y3) - q1 * D(y2) * 2**256 - q0 * D(y2)         - selEq[6]

    ===============  384 bits =======================================

    EQ12:  A(x1) * B(y1) + C(x2) - op(y3) - q1 * D(y2) * 2**256 - q0 * D(y2)         - selEq[7]

    EQ13:  x1 * x2 - y1 * y2 - x3 + (q1 * p2)                    - selEq[8]
    EQ14:  y1 * x2 + x1 * y2 - y3 + (q2 * p2)                    - selEq[8]

	EQ15:  x1 + x2 - x3 + (q1 * p2)                              - selEq[9]
	EQ16:  y1 + y2 - y3 + (q2 * p2)                              - selEq[9]

	EQ17:  x1 - x2 - x3 + (q1 * p2)                              - selEq[10]
	EQ18:  y1 - y2 - y3 + (q2 * p2)                              - selEq[10]

	EQ19:  256to384                                              - selEq[11]


    where p1 refers to the base field order of:
        Â· Secp256k1:  0xFFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFE FFFF FC2F
    and p2 refers to the base field order of:
        Â· BN254:      0x3064 4E72 E131 A029 B850 45B6 8181 585D 9781 6A91 6871 CA8D 3C20 8C16 D87C FD47
*/
include "global.pil";

namespace Arith(%N);

    pol constant BYTE2_BIT21;
    pol constant SEL_BYTE2_BIT21;
    pol constant GL_SIGNED_22BITS;

    pol commit x1[16];
    pol commit y1[16];
    pol commit x2[16];
    pol commit y2[16];
    pol commit x3[16];
    pol commit y3[16];

    pol commit s[16];
    pol commit q0[16];
    pol commit q1[16];
    pol commit q2[16];

    pol commit y2_clock;
    pol commit x3y3_clock;

    pol commit resultEq;

    resultEq * (1 - resultEq) = 0;

    // FEATURE:
    //      Â· Verify that the x1,x2 are different when adding different elliptic curve points (EQ1,EQ3,EQ4).

    // PRE:
    //      Â· x1,x2,y1,y2 are alias free for EQ[1..10]

    // In the first 16 clocks of 32-clock cycle we have: x2[0]-x1[0], x2[1]-x1[1], ..., x2[15]-x1[15]
    // In the last  16 clocks of 32-clock cycle we set all values to 0.
    pol xDeltaChunk = <%- clksel(['x2[0..15]',], 'Global.CLK32') %>
                    - <%- clksel(['x1[0..15]'], 'Global.CLK32', ' - ') %>;

    // Inverse of current xDeltaChunk. Used to verify that xDeltaChunk is â‰  0, i.e., when x1[i] â‰  x2[i].
    // This inverse only needs to be calculated one time by cycle.
    pol commit xDeltaChunkInverse;

    // Intermediate pol to indicate that x1 is different from x2 in the current chunk.
    // IMPORTANT: xChunkDifferent == 1 ==> x1[i] â‰  x2[i]
    //            xChunkDifferent == 0 does not imply anything, x1[i] â‰  x2[i] or x1[i] == x2[i].
    // xChunkDifferent was binary, only when xDeltaChunkInverse === inv(xDeltaChunk), its value is 1.
    pol xChunkDifferent = xDeltaChunk * xDeltaChunkInverse;

    // Important, binary constraint
    xChunkDifferent * (1 - xChunkDifferent) = 0;

    // xDeltaChunk  xDeltaChunkInverse  xChunkDifferent
    // -----------  ------------------  -----------------------
    //       0             any          0 * any = 0            OK (A)
    //   x (â‰  0)           0            x * 0 = 0              OK (B)
    //   x (â‰  0)         inv(x)         x * inv(x) = 1         OK (C)
    //   x (â‰  0)      y â‰  {inv(x),0}    x * y = k (â‰  0,1)      FAILS (D)
    //
    // (A) and (C) are the standard cases, but (B) was introduced to avoid calculate the inverse when
    // it is not necessary. xChunkDifferent is well calculed because it's 1 only when x â‰  0 and inverse
    // it's calculated.

    // xAreDifferent is used to indicate that in current clock we knows that x1 and x2 are different
    pol commit xAreDifferent;

    // Binary constraint
    xAreDifferent * (1 - xAreDifferent) = 0;

    // In first clock of 16-clock cycle, xAreDifferent should not be considered
    // Constraint to force next value of xAreDifferent

    xAreDifferent' = xAreDifferent * (1 - Global.CLK32[0] - Global.CLK32[16]) + xChunkDifferent;

    // xAreDifferent  CLK0+CLK16  xChunkDifferent  xAreDifferent'
    // -------------  ----------  ---------------  --------------
    //        0           0              0            0 OK
    //        0           0              1            1 OK
    //        0           1              0            1 OK
    //        0           1              1            1 OK
    //        1           0              0            1 OK
    //        1           0              1            2 FAILS (A)
    //        1           1              0            1 OK
    //        1           1              1            1 OK
    //
    // xChunkDifferent = 0 ==> (x2[i] - x1[i]) = 0
    // xChunkDifferent = 1 ==> (x2[i] - x1[i]) â‰  0  ==> x1 and x2 are different
    // (A) forbidden, executor must control this situation, because expending an
    // unnecessary inverse.

    (xAreDifferent - selEq[1]) * Global.CLK32[16] = 0;

    // COST:   2 commit, 2 im, 0 constant, 4 constraints


    // FEATURE:
    //
    // Verify that the result of Secp256k1 addition (EQ1/EQ2,EQ3,EQ4) is free of aliases, i.e. that x3,y3 âˆŠ [0, p-1]
    // Verify that the result of ð”½pÂ² = ð”½p[u]/(uÂ² + 1) multiplication, addition and subtraction (EQ5,EQ6 || EQ7,EQ8 || EQ9,EQ10)
    // is free of aliases, i.e. that x3,y3 âˆŠ [0, p-1]
    //
    // Here, p refers, respectively, to the base field order of any of the following elliptic curves:
    //
    // Secp256k1:  0xFFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFF FFFE FFFF FC2F
    // BN254:      0x3064 4E72 E131 A029 B850 45B6 8181 585D 9781 6A91 6871 CA8D 3C20 8C16 D87C FD47
    //
    // Strategy: Compare from the most significant chunk to the least significant chunk of x3 and y3 (A). On each chunk,
    // chunkLtPrime indicates whether the chunk is smaller than the FPEC chunk. valueLtPrime indicates if any
    // previous chunk was smaller than the FPEC chunk, which implies that x3 and y3 are smaller than prime.
    //
    // (A) In the first 16 clocks of 32-clock cycle, it is verified that x3 âˆŠ [0, p-1]
    //     In the last  16 clocks of 32-clock cycle, it is verified that y3 âˆŠ [0, p-1]
    //
    //    k x chunks equals (B) ==> 1 x chunk lower ==> (15 - k) x any 16 bits value
    //
    // (B) I must verify that the chunks are equal or smaller, never greater at this point. If a malicious prover (MP)
    // claims they are equal when they are actually smaller, that's its issue. At the end, there must be at least one
    // smaller chunk to ensure a smaller value.
    //
    //    k x chunks = chunkPrime ==> 1 x chunk âˆŠ [0, chunkPrime-1] ==> (15 - k) x chunk âˆŠ [0, 0xFFFF]
    //
    //                             CLK0    CLK1    CLK2    CLK3    CLK4    CLK5    CLK6    CLK7    CLK8    CLK9   CLK10   CLK11   CLK12   CLK13   CLK14   CLK15
    //   chunkPrime(0..15)     = [0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFF, 0xFFFE, 0xFFFF, 0xFC2F]
    //   chunkPrime(0..15) - 1 = [0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFE, 0xFFFD, 0xFFFE, 0xFC2E]
    //   chunkPrime(0..15)     = [0x3064, 0x4e72, 0xe131, 0xa029, 0xb850, 0x45b6, 0x8181, 0x585d, 0x9781, 0x6a91, 0x6871, 0xca8d, 0x3c20, 0x8c16, 0xd87c, 0xfd47]
    //   chunkPrime(0..15) - 1 = [0x3063, 0x4e71, 0xe130, 0xa028, 0xb84f, 0x45b5, 0x8180, 0x585c, 0x9780, 0x6a90, 0x6870, 0xca8c, 0x3c1f, 0x8c15, 0xd87b, 0xfd46]
    //
    //   different Secp256k1 prime chunks [0xFFFF, 0xFFFE, 0xFFFD, 0xFC2F, 0xFC2E]
    //   different BN254 prime chunks [0x3064, 0x4e72, 0xe131, 0xa029, 0xb850, 0x45b6, 0x8181, 0x585d, 0x9781, 0x6a91, 0x6871, 0xca8d, 0x3c20, 0x8c16, 0xd87c, 0xfd47,
    //                                 0x3063, 0x4e71, 0xe130, 0xa028, 0xb84f, 0x45b5, 0x8180, 0x585c, 0x9780, 0x6a90, 0x6870, 0xca8c, 0x3c1f, 0x8c15, 0xd87b, 0xfd46]

    //              CLK0   CLK1  CLK2  CLK3  CLK4  CLK5  CLK6  CLK7  CLK8  CLK9  CLK10 CLK11 CLK12 CLK13 CLK14 CLK15
    // Secp256k1:  0xFFFF  FFFF  FFFF  FFFF  FFFF  FFFF  FFFF  FFFF  FFFF  FFFF  FFFF  FFFF  FFFF  fffe  FFFF  fc2f
    // BN254:      0x3064  4e72  e131  a029  b850  45b6  8181  585d  9781  6a91  6871  ca8d  3c20  8c16  d87c  fd47
    //
    //                 RANGE_SEL  P_RANGE     BASE
    //              /      0      [0,0xFFFF]  CLK0, ..., CLK12, CLK14
    //              |      1      [0,0xFFFE]  CLK13
    //   Secp256k1  |      2      [0,0xFFFD]
    //              |      3      [0,0xFC2F]  CLK15
    //              \      4      [0,0xFC2E]
    //              /      5      [0,0x3064]  CLK0
    //              |      6      [0,0x3063]
    //              |      7      [0,0x4e72]  CLK1
    //              |      8      [0,0x4e71]
    //              |      9      [0,0xe131]  CLK2
    //              |     10      [0,0xe130]
    //              |     11      [0,0xa029]  CLK3
    //              |     12      [0,0xa028]
    //              |     13      [0,0xb850]  CLK4
    //              |     14      [0,0xb84f]
    //              |     15      [0,0x45b6]  CLK5
    //              |     16      [0,0x45b5]
    //              |     17      [0,0x8181]  CLK6
    //              |     18      [0,0x8180]
    //              |     19      [0,0x585d]  CLK7
    //    BN254     |     20      [0,0x585c]
    //              |     21      [0,0x9781]  CLK8
    //              |     22      [0,0x9780]
    //              |     23      [0,0x6a91]  CLK9
    //              |     24      [0,0x6a90]
    //              |     25      [0,0x6871]  CLK10
    //              |     26      [0,0x6870]
    //              |     27      [0,0xca8d]  CLK11
    //              |     28      [0,0xca8c]
    //              |     29      [0,0x3c20]  CLK12
    //              |     30      [0,0x3c1f]
    //              |     31      [0,0x8c16]  CLK13
    //              |     32      [0,0x8c15]
    //              |     33      [0,0xd87c]  CLK14
    //              |     34      [0,0xd87b]
    //              |     35      [0,0xfd47]  CLK15
    //              \     36      [0,0xfd46]
    //
    //   pRangeSecp256k1 = (CLK13 + 3 * CLK15 + chunkLtPrime) * (selEq[1] + selEq[2])
    //   pRangeBN254 = (5 * CLK0 + 7 * CLK1 + 9 * CLK2 + 11 * CLK3 + 13 * CLK4 + 15 * CLK5 + 17 * CLK6 + 19 * CLK7 + 21 * CLK8 + 23 * CLK9 + 25 * CLK10 + 27 * CLK11 + 29 * CLK12 + 31 * CLK13 + 33 * CLK14 + 35 * CLK15) * (selEq[4] + selEq[5] + selEq[6])
    //   pRange =  (pRangeSecp256k1 + pRangeBN254) * (1 - valueLtPrime)
    //
    //   CLK                pRange                            pRange
    //           [selEq[1] + selEq[2] = 1, chunkLtPrime = 0]   [selEq[1] + selEq[2] = 1, chunkLtPrime = 1]
    //
    //
    //   0              0 + 3 * 0 + 0 = 0               0 + 3 * 0 + 1 = 1
    //   :                      :                               :
    //   12             0 + 3 * 0 + 0 = 0               0 + 3 * 0 + 1 = 1
    //   13             1 + 3 * 0 + 0 = 1               1 + 3 * 0 + 1 = 2
    //   14             0 + 3 * 0 + 0 = 0               0 + 3 * 0 + 1 = 1
    //   15             0 + 3 * 1 + 0 = 3               0 + 3 * 1 + 1 = 4
    //
    //   valueLtPrime' = valueLtPrime * (1 - CLK0 - CLK16) + chunkLtPrime
    //
    //   valueLtPrime  chunkLtPrime  CLK0+CLK16  valueLtPrime'
    //
    //         0             0           0            0            OK
    //         0             1           0            1            OK
    //         1             0           0            1            OK
    //         1             1           0            2            FAILS, valueLtPrime not binary (D)
    //
    //         0             0           1            0            OK
    //         0             1           1            1            OK
    //         1             0           1            0            OK
    //         1             1           1            1            OK (E)
    //
    //   (D) when valueLtPrime = 0 ==> chunkLtPrime must be zero, if not it FAILS
    //   (E) on clocks 0 and 16, current value of valueLtPrime isn't considered
    //
    //
    //    Examples (Secp256k1 prime):
    //                                â”Œâ”€â”€â”€â”€â”€â”€â”€â”€ pRange
    //              â”Œâ”€â”€ max_value     â”‚  â”Œâ”€â”€â”€â”€â”€ chunkLtPrime
    //              â”‚     â”Œâ”€ example  â”‚  â”‚  â”Œâ”€â”€ valueLtPrime
    //              â”‚     â”‚           â”‚  â”‚  â”‚
    //    CLK0    0xFFFF  0xFFFF      0  0  0
    //    CLK1    0xFFFF  0xFF00      1  1  0  // RANGE [0, 0xFFFE]
    //    CLK2    0xFFFF  0x0000      0  0  1
    //    CLK3    0xFFFF  0x0000      0  0  1
    //    CLK4    0xFFFF  0xFFFF      0  0  1
    //    :
    //    CLK15   0xFC2F  0xFFFF      0  0  1
    //    CLK16   0xFFFF  0xFFFF      0  0  1
    //
    //
    //                                â”Œâ”€â”€â”€â”€â”€â”€â”€â”€ pRange
    //              â”Œâ”€â”€ max_value     â”‚  â”Œâ”€â”€â”€â”€â”€ chunkLtPrime
    //              â”‚     â”Œâ”€ example  â”‚  â”‚  â”Œâ”€â”€ valueLtPrime
    //              â”‚     â”‚           â”‚  â”‚  â”‚
    //    CLK0    0xFFFF  0xFFFF      0  0  0
    //    CLK1    0xFFFF  0xFFFF      0  0  0
    //    CLK2    0xFFFF  0xFFFF      0  0  0
    //    :
    //    CLK12   0xFFFF  0xFFFF      0  0  0
    //    CLK13   0xFFFE  0xFFFF      1  0  0    // CLK13 == 1 RANGE [0, 0xFFFE]
    //    CLK14   0xFFFF  0xFFFF      0  0  0
    //    CLK15   0xFC2F  0xFC00      4  1  0    // CLK15 == 1 RANGE [0, 0xFC2F]
    //    CLK16   0xFFFF  0xFFFF      0  0  1

    // valueLtPrime indicates whether we know that the value is smaller than the prime.
    // If selEq[1]+selEq[2],selEq[3],selEq[4],selEq[5],selEq[6] = 0, it must be 0.
    pol commit valueLtPrime;

    // how range is too big, we divide chunckLtPrime in two flags, highChunckLtPrime and lowChunkLtPrime.
    // highChunkLtPrime indicates whether the high part (bits: 23-16) of current value chunk is smaller than the high prime chunk.
    // lowChunkLtPrime indicates whether the low part (bits: 0-15) of current value chunk is smaller than the low prime chunk.
    // If selEq[1]+selEq[2],selEq[3],selEq[4],selEq[5],selEq[6] = 0 or valueLtPrime = 1, it must be 0.
    pol commit chunkLtPrime;

    // binary constraints
    valueLtPrime * (1 - valueLtPrime) = 0;
    chunkLtPrime * (1 - chunkLtPrime) = 0;

    // This constraint applies to the next value of valueLtPrime. At the beginning of each 16-clock cycle,
    // valueLtPrime is ignored because it refers to the previous value.
    valueLtPrime' = valueLtPrime * (1 - Global.CLK32[0] - Global.CLK32[16]) + chunkLtPrime;

    // This constraint is used to verify (and to enforce) that at the end of a 16-clock cycle, valueLtPrime is equal to 1.
    // This constraint is applied when one of selEq[1..6] equals 1.
    (valueLtPrime' - selEq[1] - selEq[2] - selEq[3] - selEq[4] - selEq[5] - selEq[6]) * (Global.CLK32[15] + Global.CLK32[31]) = 0;


    // TODO: CLK32[32] ==> CLK32C[16] (using ')

    // range to be looked up for each chunk and for each prime
    // pol pRangeSecp256k1 = (Global.CLK32[13] + Global.CLK32[29] + 3 * (Global.CLK32[15] + Global.CLK32[31]) + lowChunkLtPrime) * (selEq[1] + selEq[2]);

    // pol constant INDEX;  // [0..15]
    
    //pol pRange =  1 + (selEq[1] + selEq[2]) + 
    //             33 * (selEq[3] + selEq[4] + selEq[5]) + 
    //             65 * (selEq[8] + selEq[9] + selEq[10]) + 
    //              2 * INDEX + lowChunkLtPrime; 
    
    // {pRange * (1 - valueLtPrime), lsc_x3y3} in {L_RANGE_SEL, Global.BYTE2};
    // {(pRange + highChunkLtPrime) * (1 - valueLtPrime), hsc_x3y3} in {H_RANGE_SEL, Global.BYTE};

    // pol low_pRangeBLS12_381  = (pRangeBLS12_381 + lowChunkLtPrime) * (selEq[8] + selEq[9] + selEq[10]);
    // pol high_pRangeBLS12_381 = (pRangeBLS12_381 + highChunkLtPrime) * (selEq[8] + selEq[9] + selEq[10]);

    // Correctness of the y2_clock, x3_clock and y3_clock
    <%- clksel(['y2[15..0]','y2[15..0]'], 'Global.CLK32') %> - y2_clock = 0;
    <%- clksel(['x3[15..0]','y3[15..0]'], 'Global.CLK32') %> - x3y3_clock = 0;

    /****
    *
    * LATCH POLS: x1,y1,x2,y2,x3,y3,s,q0,q1,q2
    *
    *****/

    <%- latch('x1[0..15]','Global.CLK32[31]') %>

    <%- latch('y1[0..15]','Global.CLK32[31]') %>

    <%- latch('x2[0..15]','Global.CLK32[31]') %>

    <%- latch('y2[0..15]','Global.CLK32[31]') %>

    <%- latch('x3[0..15]','Global.CLK32[31]') %>

    <%- latch('y3[0..15]','Global.CLK32[31]') %>

    <%- latch('s[0..15]','Global.CLK32[31]') %>

    <%- latch('q0[0..15]','Global.CLK32[31]') %>

    <%- latch('q1[0..15]','Global.CLK32[31]') %>

    <%- latch('q2[0..15]','Global.CLK32[31]') %>

    //  RANGE CHECK x1,y1,x2,y2,s,q0,q1,q2

    pol commit hs_bit_x1y1, hsc_x1y1, lsc_x1y1;
    pol commit hsc_x2y2, lsc_x2y2;
    pol commit hsc_sq0q1,  lsc_sq0q1;
    pol commit hsc_q1q2qh, lsc_q1q2qh;
    pol commit hsc_x3y3, lsc_x3y3;   
    
    // divide range check in two chunks of high significant chunk (hsc_xx) and less significant chunk (lsc_xx)
    // constrints to 

    <%- clksel(['x1[0..15]','y1[0..15]'], 'Global.CLK32') %> = hs_bit_x1y1 * 2**23 + hsc_x1y1 * 2**16 + lsc_x1y1;
    <%- clksel(['x2[0..15]','y2[0..15]'], 'Global.CLK32') %> = hsc_x2y2 * 2**16 + lsc_x2y2;
    <%- clksel(['x3[15..0]','y3[15..0]'], 'Global.CLK32') %> = hsc_x3y3 * 2**16 + lsc_x3y3;
    <%- clksel([ 's[0..15]','q0[0..14]','q1[0]'], 'Global.CLK32') %> = hsc_sq0q1 * 2**16 + lsc_sq0q1;
    <%- clksel(['q1[1..14]','q2[0..15]','q0[15]', 'q1[15]'], 'Global.CLK32') %> = hsc_q1q2qh * 2**16 + lsc_q1q2qh;

    pol commit hs_bit_delta, ls_bits_delta;
    pol commit limit_chunk;

    pol constant LIMIT_SECP256K1;   // cycle: 16 chunks x 16 bits of prime curve Secp256k1 from high significant chunk to less significant chunk
                                    // [0xFFFF:13, 0xFFFE, 0xFFFF, 0xFC2F]

    pol constant LIMIT_BN254;       // cycle: 16 chunks x 16 bits of prime curve BN254 from high significant chunk to less significant chunk
                                    // [0x3064, 0x4E72, 0xE131, 0xA029, 0xB850, 0x45B6, 0x8181, 0x585D, 
                                    //  0x9781, 0x6A91, 0x6871, 0xCA8D, 0x3C20, 0x8C16, 0xD87C, 0xFD47]...

    pol constant LIMIT_BLS12381;    // cycle: 16 chunks x 24 bits of prime curve BLS12-381 from high significant chunk to less significant chunk
                                    // [0x1A0111, 0xEA397F, 0xE69A4B, 0x1BA7B6, 0x434BAC, 0xD76477, 0x4B84F3, 0x8512BF, 
                                    //  0x6730D2, 0xA0F6B0, 0xF6241E, 0xABFFFE, 0xB153FF, 0xFFB9FE, 0xFFFFFF, 0xFFAAAB]...

    limit_chunk = (selEq[1] + selEq[2]) * LIMIT_SECP256K1 + (selEq[3] + selEq[4] + selEq[5]) * LIMIT_BN254 + 
                  (selEq[7] + selEq[8] + selEq[9]) * LIMIT_BLS12381 + (selEq[6] + selEq[7]) * y2_clock + 
                  (selEq[0] + selEq[11]) * x3y3_clock;

    // selEq[0] o selEq[11] => chunkLtPrime = 0, limit_chunk = x3y3_clock, hs_bit_delta = 0, ls_bits_delta = 0

    // delta = limit - x3y3_clock - chunkLtPrime ==> delta - limit + x3y3_clock + chunkLtPrime = 0
    // delta >= 0 ==> delta â‚¬ [0, 2**24)

    (1 - valueLtPrime) * (hs_bit_delta * 2**23 + ls_bits_delta - limit_chunk + x3y3_clock + chunkLtPrime) = 0;
        
    // check valueLtPrime is one at end of each 16 clocks for all operations that control limit of x3,y3.
    (valueLtPrime' - selEq[1] - selEq[2] - selEq[3] - selEq[4] - selEq[5] - selEq[8] - selEq[9] - selEq[10]) * (Global.CLK32[15] + Global.CLK32[31]) = 0;

    ls_bits_delta in Global.STEP;
    hs_bit_delta * (1 - hs_bit_delta) = 0;

    // check less significant chunks are 16 bits chunk
    lsc_x1y1 in Global.BYTE2;
    lsc_x2y2 in Global.BYTE2;
    lsc_x3y3 in Global.BYTE2;
    lsc_sq0q1  in Global.BYTE2;
    lsc_q1q2qh in Global.BYTE2;

    // BLS12-381 arith + modular 384 (no includes 256To384)
    pol arith384 = selEq[7] + selEq[8] + selEq[9] + selEq[10];

    pol arith256to386 = selEq[11];
    // 256TO384 A(x1),B(y1) 256 bits  op(y3) 384 bits

    // when operation no use fully (*) 384 bits, high part must be 0, because value must be < 2**16 
    hs_bit_x1y1 * (1 - arith384) = 0;
    hsc_x1y1 * (1 - arith384) = 0;
    hsc_x2y2 * (1 - arith384) = 0; 
    hsc_x3y3 * (1 - arith384) = 0; 

    // s,q0,q1,q2 could be 24 bits Â¿enought for arith384 ?
    hsc_sq0q1 * (1 - arith384) = 0;  
    hsc_q1q2qh * (1 - arith384) = 0;  


    // (*) in case of arith to prove 256TO384 no use fully 384 bits, because only op (x3) work with 384 bits.
    // 256TO384 don't use x3, for this reason not matter that it's 256 or 384 bits.
    hsc_x3y3 * (1 - arith384 - arith256to386) = 0;  

    pol constant BITS7_BYTE2;
    pol constant BYTE2_BIT14_SEL;
    pol constant BYTE2_BIT14;

    // BITS7_BYTE2: 7 bits + Global.BYTE: 8 bits + Global.BYTE_2A: 8 bits = 23 bits 
    // hsc_x1y1 has only 7 bits, the 8th bit was on hs_bit_x1y1
    // NOTE: done in this way because it's more cheaper 3 lookup in only one
    {hsc_x1y1, hsc_x2y2, hsc_x3y3} in {BITS7_BYTE2, Global.BYTE, Global.BYTE_2A};

    
    // TODO: review range checks of Qs
    // BYTE2_BIT14_SEL: 1 bit + Global.BYTE: 8 bits + BYTE2_BIT14: 14 bits = 23 bits 
    // low part: 16 + high part: 14 bits = 30 bits (REVIEW for 384 aritmetics)
    { Global.CLK32[29] + Global.CLK32[30] + Global.CLK32[31], hsc_sq0q1, hsc_q1q2qh} in {BYTE2_BIT14_SEL, Global.BYTE, BYTE2_BIT14};


    /*******
    *
    * EQ0: A(x1) * B(y1) + C(x2) = D (y2) * 2 ** 256 + op (y3)  # selEq[0] [256 bits]
    *
    *******/

    <%- equation('pol eq0_## =', 'x1*y1-p2_256*y2+x2-y3', {p2_256: 0x10000000000000000000000000000000000000000000000000000000000000000n}) %>

    /*******
    *
    * EQ1: s * x2 - s * x1 - y2 + y1 - p * (q0 - offset) = 0    # selEq[1] [SECP256K1, 256 bits]
    *
    *******/

    <%- equation('pol eq1_## =', 's*x2-s*x1-y2+y1-p*q0+p*offset', {p: 0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffefffffc2fn,
                                                                   offset:0x20000000000000000000000000000000000000000000000000000000000000000n}) %>

    /*******
    *
    * EQ2:  2 * s * y1 - 3 * x1 * x1 + p * (q0 - offset) = 0    # selEq[2] [SECP256K1, 256 bits]
    *
    *******/

    <%- equation('pol eq2_## =', 's*y1+s*y1-x1*x1-x1*x1-x1*x1+p*q0-p*offset', {p: 0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffefffffc2fn,
                                                                               offset:0x40000000000000000000000000000000000000000000000000000000000000000n}) %>

    /*******
    *
    * EQ3:  s * s - x1 - x2 - x3 - p * (q1 - offset) = 0        # selEq[1]+selEq[2] [SECP256K1, 256 bits]
    *
    *******/

    <%- equation('pol eq3_## =', 's*s-x1-x2-x3-p*q1+p*offset', {p: 0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffefffffc2fn,
                                                                offset:0x4n}) %>

    /*******
    *
    * EQ4:  s * x1 - s * x3 - y1 - y3 + p * (q2 - offset) = 0   # selEq[1]+selEq[2] [SECP256K1, 256 bits]
    *
    *******/

    <%- equation('pol eq4_## =', 's*x1-s*x3-y1-y3+p*q2-p*offset', {p: 0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffefffffc2fn,
                                                                   offset:0x20000000000000000000000000000000000000000000000000000000000000000n}) %>

    /*******
    *
    * EQ5:  x1 * x2 - y1 * y2 - x3 + p * (q1 - offset) = 0      # selEq[3] [BN254, 256 bits]
    *
    *******/

    <%- equation('pol eq5_## =', 'x1*x2-y1*y2-x3+p*q1-p*offset', {p: 21888242871839275222246405745257275088696311157297823662689037894645226208583n,
                                                                  offset:0x80000000000000000000000000000000000000000000000000000000000000000n}) %>

    /*******
    *
    * EQ6:  y1 * x2 + x1 * y2 - y3 - p * (q2 - offset) = 0      # selEq[3] [BN254, 256 bits]
    *
    *******/

    <%- equation('pol eq6_## =', 'y1*x2+x1*y2-y3-p*q2+p*offset', {p: 21888242871839275222246405745257275088696311157297823662689037894645226208583n,
                                                                  offset: 0x8n}) %>

    /*******
    *
    * EQ7:  x1 + x2 - x3 - p * (q1 - offset) = 0                # selEq[4] [BN254, 256 bits]
    *
    *******/

    <%- equation('pol eq7_## =', 'x1+x2-x3-p*q1+p*offset', {p: 21888242871839275222246405745257275088696311157297823662689037894645226208583n,
                                                            offset: 0x8n}) %>

    /*******
    *
    * EQ8:  y1 + y2 - y3 - p * (q2 - offset) = 0                # selEq[4] [BN254, 256 bits]
    *
    *******/

    <%- equation('pol eq8_## =', 'y1+y2-y3-p*q2+p*offset', {p: 21888242871839275222246405745257275088696311157297823662689037894645226208583n,
                                                            offset: 0x8n}) %>

    /*******
    *
    * EQ9:  x1 - x2 - x3 + p * (q1 - offset) = 0                # selEq[5] [BN254, 256 bits]
    *
    *******/

    <%- equation('pol eq9_## =', 'x1-x2-x3+p*q1-p*offset', {p: 21888242871839275222246405745257275088696311157297823662689037894645226208583n,
                                                            offset: 0x8n}) %>

    /*******
    *
    * EQ10:  y1 - y2 - y3 + p * (q2 - offset) = 0               # selEq[5] [BN254, 256 bits]
    *
    *******/

    <%- equation('pol eq10_## =', 'y1-y2-y3+p*q2-p*offset', {p: 21888242871839275222246405745257275088696311157297823662689037894645226208583n,
                                                             offset: 0x8n}) %>

    /*******
    *
    * EQ11:  A(x1) * B(y1) + C(x2) - op(y3) - q1 * D(y2) * 2**256 - q0 * D(y2) # selEq[6] [256 bits]
    *
    *******/
    <%- equation('pol eq11_## =', 'x1*y1+x2-y3-q1*y2*powOfTwo-q0*y2', {powOfTwo: 0x10000000000000000000000000000000000000000000000000000000000000000n}) %>


    /*******
    *
    * EQ12:  A(x1) * B(y1) + C(x2) - op(y3) - q1 * D(y2) * 2**256 - q0 * D(y2) # selEq[7] [384 bits]
    *
    *******/
    <%- equation('pol eq12_## =', 'x1*y1+x2-y3-q1*y2*powOfTwo-q0*y2', {powOfTwo: 0x10000000000000000000000000000000000000000000000000000000000000000n}, {chunkBits: 24}) %>

    
    /*******
    *
    * EQ13:  x1 * x2 - y1 * y2 - x3 + p * (q1 - offset) = 0     # selEq[8] [BLS12-381, 384 bits]
    *
    *******/

    <%- equation('pol eq13_## =', 'x1*x2-y1*y2-x3+p*q1-p*offset', {p: 0x1a0111ea397fe69a4b1ba7b6434bacd764774b84f38512bf6730d2a0f6b0f6241eabfffeb153ffffb9feffffffffaaabn,
                                                                  offset:0x80000000000000000000000000000000000000000000000000000000000000000n}, {chunkBits: 24}) %>

    /*******
    *
    * EQ14:  y1 * x2 + x1 * y2 - y3 - p * (q2 - offset) = 0     # selEq[8] [BLS12-381, 384 bits]
    *
    *******/

    <%- equation('pol eq14_## =', 'y1*x2+x1*y2-y3-p*q2+p*offset', {p: 0x1a0111ea397fe69a4b1ba7b6434bacd764774b84f38512bf6730d2a0f6b0f6241eabfffeb153ffffb9feffffffffaaabn,
                                                                  offset: 0x8n}, {chunkBits: 24}) %>

    /*******
    *
    * EQ15:  x1 + x2 - x3 - p * (q1 - offset) = 0               # selEq[9] [BLS12-381, 384 bits]
    *
    *******/

    <%- equation('pol eq15_## =', 'x1+x2-x3-p*q1+p*offset', {p: 0x1a0111ea397fe69a4b1ba7b6434bacd764774b84f38512bf6730d2a0f6b0f6241eabfffeb153ffffb9feffffffffaaabn,
                                                            offset: 0x8n}, {chunkBits: 24}) %>

    /*******
    *
    * EQ16:  y1 + y2 - y3 - p * (q2 - offset) = 0               # selEq[9] [BLS12-381, 384 bits]
    *
    *******/

    <%- equation('pol eq16_## =', 'y1+y2-y3-p*q2+p*offset', {p: 0x1a0111ea397fe69a4b1ba7b6434bacd764774b84f38512bf6730d2a0f6b0f6241eabfffeb153ffffb9feffffffffaaabn,
                                                            offset: 0x8n}, {chunkBits: 24}) %>

    /*******
    *
    * EQ17:  x1 - x2 - x3 + p * (q1 - offset) = 0               # selEq[10] [BLS12-381, 384 bits]
    *
    *******/

    <%- equation('pol eq17_## =', 'x1-x2-x3+p*q1-p*offset', {p: 0x1a0111ea397fe69a4b1ba7b6434bacd764774b84f38512bf6730d2a0f6b0f6241eabfffeb153ffffb9feffffffffaaabn,
                                                            offset: 0x8n}, {chunkBits: 24}) %>

    /*******
    *
    * EQ18:  y1 - y2 - y3 + p * (q2 - offset) = 0               # selEq[10] [BLS12-381, 384 bits]
    *
    *******/

    <%- equation('pol eq18_## =', 'y1-y2-y3+p*q2-p*offset', {p: 0x1a0111ea397fe69a4b1ba7b6434bacd764774b84f38512bf6730d2a0f6b0f6241eabfffeb153ffffb9feffffffffaaabn,
                                                             offset: 0x8n}, {chunkBits: 24}) %>
    
    /*******
    *
    * EQ19:  A(x1) + 2 ** 256 * B(y1)  = op (y3)                # selEq[11] [A,B: 256 bits / op: 384 bits]
    *
    *******/


    pol eq19_0 = x1[0] + 2**16 * x1[1] - y3[0];
    pol eq19_1 = x1[2] + 2**16 - y3[1];
    pol eq19_2 = x1[3] + 2**16 * x1[4] - y3[2];
    pol eq19_3 = x1[5] + 2**16 - y3[3];
    pol eq19_4 = x1[6] + 2**16 * x1[7] - y3[4];
    pol eq19_5 = y1[0] + 2**16 - y3[5];
    pol eq19_6 = y1[1] + 2**16 * y1[2] - y3[6];
    pol eq19_7 = y1[3] + 2**16 - y3[7];

    pol eq0 = <%- clksel(['eq0_[0..31]'], 'Global.CLK32') %>;
    pol eq1 = <%- clksel(['eq1_[0..31]'], 'Global.CLK32') %>;
    pol eq2 = <%- clksel(['eq2_[0..31]'], 'Global.CLK32') %>;
    pol eq3 = <%- clksel(['eq3_[0..31]'], 'Global.CLK32') %>;
    pol eq4 = <%- clksel(['eq4_[0..31]'], 'Global.CLK32') %>;
    pol eq5 = <%- clksel(['eq5_[0..31]'], 'Global.CLK32') %>;
    pol eq6 = <%- clksel(['eq6_[0..31]'], 'Global.CLK32') %>;
    pol eq7 = <%- clksel(['eq7_[0..31]'], 'Global.CLK32') %>;
    pol eq8 = <%- clksel(['eq8_[0..31]'], 'Global.CLK32') %>;
    pol eq9 = <%- clksel(['eq9_[0..31]'], 'Global.CLK32') %>;
    pol eq10 = <%- clksel(['eq10_[0..31]'], 'Global.CLK32') %>;
    pol eq11 = <%- clksel(['eq11_[0..31]'], 'Global.CLK32') %>;
    pol eq12 = <%- clksel(['eq12_[0..31]'], 'Global.CLK32') %>;
    pol eq13 = <%- clksel(['eq13_[0..31]'], 'Global.CLK32') %>;
    pol eq14 = <%- clksel(['eq14_[0..31]'], 'Global.CLK32') %>;
    pol eq15 = <%- clksel(['eq15_[0..31]'], 'Global.CLK32') %>;
    pol eq16 = <%- clksel(['eq16_[0..31]'], 'Global.CLK32') %>;
    pol eq17 = <%- clksel(['eq17_[0..31]'], 'Global.CLK32') %>;
    pol eq18 = <%- clksel(['eq18_[0..31]'], 'Global.CLK32') %>;
    pol eq19 = <%- clksel(['eq19_[0..7]'], 'Global.CLK32') %>;


    // Equation selectors. Must be binary, constant during the operation and operation-exclusive
    pol commit selEq[12];

    <%- latch(['selEq[0..11]'],'Global.CLK32[31]') %>
    <%- binary(['selEq[0..11]']) %>
    pol allSelEq = selEq[0] + selEq[1] + selEq[2] + selEq[3] + selEq[4] + selEq[5] + selEq[6] + selEq[7] + selEq[8] + selEq[9] + selEq[10] + selEq[11];

    <%- binary(['allSelEq']) %>
    pol commit carry[3];

    carry[0] * Global.CLK32[0] = 0;
    carry[1] * Global.CLK32[0] = 0;
    carry[2] * Global.CLK32[0] = 0;

    // TODO: review with 384
    carry[0] in GL_SIGNED_22BITS;
    carry[1] in GL_SIGNED_22BITS;
    carry[2] in GL_SIGNED_22BITS;

    selEq[0] * (eq0 + carry[0]) = selEq[0] * carry[0]' * 2**16;

    selEq[1] * (eq1 + carry[0]) = selEq[1] * carry[0]' * 2**16;
    selEq[2] * (eq2 + carry[0]) = selEq[2] * carry[0]' * 2**16;
    (selEq[1] + selEq[2]) * (eq3 + carry[1]) = (selEq[1] + selEq[2]) * carry[1]' * 2**16;
    (selEq[1] + selEq[2]) * (eq4 + carry[2]) = (selEq[1] + selEq[2]) * carry[2]' * 2**16;

    selEq[3] * (eq5 + carry[0]) = selEq[3] * carry[0]' * 2**16;
    selEq[3] * (eq6 + carry[1]) = selEq[3] * carry[1]' * 2**16;

    selEq[4] * (eq7 + carry[0]) = selEq[4] * carry[0]' * 2**16;
    selEq[4] * (eq8 + carry[1]) = selEq[4] * carry[1]' * 2**16;

    selEq[5] * (eq9 + carry[0]) = selEq[5] * carry[0]' * 2**16;
    selEq[5] * (eq10 + carry[1]) = selEq[5] * carry[1]' * 2**16;

    selEq[6] * (eq11 + carry[0]) = selEq[6] * carry[0]' * 2**16;

    selEq[7] * (eq12 + carry[0]) = selEq[7] * carry[0]' * 2**24;

    selEq[8] * (eq13 + carry[0]) = selEq[8] * carry[0]' * 2**24;
    selEq[8] * (eq14 + carry[1]) = selEq[8] * carry[1]' * 2**24;

    selEq[9] * (eq15 + carry[0]) = selEq[9] * carry[0]' * 2**24;
    selEq[9] * (eq16 + carry[1]) = selEq[9] * carry[1]' * 2**24;

    selEq[10] * (eq17 + carry[0]) = selEq[10] * carry[0]' * 2**24;
    selEq[10] * (eq18 + carry[1]) = selEq[10] * carry[1]' * 2**24;

    selEq[11] * (eq19 + carry[0]) = selEq[11] * carry[0]' * 2**24;
